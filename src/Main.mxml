<?xml version="1.0" encoding="utf-8"?>
<!--
///////////////////////////////////////////////////////////////////////////////////
// 
//  Copyright (c) 2014 <nailsonnego@gmail.com>
// 
//  Permission is hereby granted, free of charge, to any person obtaining a copy
//  of this software and associated documentation files (the "Software"), to deal
//  in the Software without restriction, including without limitation the rights
//  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
//  copies of the Software, and to permit persons to whom the Software is
//  furnished to do so, subject to the following conditions:
// 
//  The above copyright notice and this permission notice shall be included in
//  all copies or substantial portions of the Software.
// 
//  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
//  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
//  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
//  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
//  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
//  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
//  THE SOFTWARE.
//
///////////////////////////////////////////////////////////////////////////////////
-->

<nail:NailMain xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   xmlns:nail="library://ns.nail.com/naillib"
			   invoke="invokeHandler(event)">
	
	<fx:Style source="css/style.css"/>
	
	<fx:Script>
		<![CDATA[
			import flash.net.registerClassAlias;
			
			import mx.core.UIComponent;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.logging.ILogger;
			import mx.logging.Log;
			
			import nail.components.controls.Alert;
			import nail.core.INailApplication;
			import nail.objectbuilder.commands.CommandType;
			import nail.objectbuilder.commands.GetSpriteListCommand;
			import nail.objectbuilder.commands.GetThingCommand;
			import nail.objectbuilder.commands.ProgressBarID;
			import nail.objectbuilder.core.ObjectBuilder;
			import nail.otlib.assets.AssetsInfo;
			import nail.otlib.components.controls.DatSprProgressBar;
			import nail.otlib.components.controls.DefaultProgressBar;
			import nail.otlib.things.ThingCategory;
			import nail.otlib.things.ThingType;
			import nail.otlib.utils.SpriteData;
			import nail.otlib.utils.ThingListItem;
			import nail.otlib.utils.ThingProperty;
			import nail.utils.FileUtils;
			import nail.workers.Command;
			
			//--------------------------------------------------------------------------
			//
			// PROPERTIES
			//
			//--------------------------------------------------------------------------
			
			[Embed(source="../workerswfs/nail/objectbuilder/core/ObjectBuilderWorker.swf", mimeType="application/octet-stream")]
			public var ObjectBuilderWorker : Class;
			private var _datSprProgressBar : DatSprProgressBar;
			private var _defaultProgressBar : DefaultProgressBar;
			private var _logger : ILogger = Log.getLogger("nail.Main");
			private var _directory : File;
			private var _errorAlert : Alert;
			
			//--------------------------------------------------------------------------
			//
			// METHODS
			//
			//--------------------------------------------------------------------------
			
			//--------------------------------------
			// Public
			//--------------------------------------
			
			public function onShowProgressBar(id:uint, title:String) : void
			{
				switch(id)
				{
					case ProgressBarID.DAT_SPR:
					{
						this.showDatSprProgressBar(title);
						break;
					}
						
					case ProgressBarID.DEFAULT:
					{
						this.showDefaultProgressBar(title);
						break;
					}
				}
			}
			
			public function onHideProgressBar(id:uint) : void
			{
				switch(id)
				{
					case ProgressBarID.DAT_SPR:
					{
						this.hideDatSprProgressBar();
						break;
					}
						
					case ProgressBarID.DEFAULT:
					{
						this.hideDefaultProgressBar();
						break;
					}
				}
			}
			
			//--------------------------------------
			// Override Public
			//--------------------------------------
			
			override public function register() : void
			{
				registerClassAlias("ThingType", ThingType);
				registerClassAlias("AssetsInfo", AssetsInfo);
				registerClassAlias("SpriteData", SpriteData);
				registerClassAlias("ByteArray", ByteArray);
				registerClassAlias("ThingProperty", ThingProperty);
				registerClassAlias("ThingListItem", ThingListItem);
				registerFunction(CommandType.ERROR, onError);
				registerFunction(CommandType.SHOW_PROGRESS_BAR, onShowProgressBar);
				registerFunction(CommandType.HIDE_PROGRESS_BAR, onHideProgressBar);
				registerFunction(CommandType.SHOW_MESSAGE, onShowMessage);
				registerFunction(CommandType.LOAD_COMPLETE, onLoadComplete);
				registerFunction(CommandType.SET_ASSETS_INFO, onSetAssetsInfo);
				registerFunction(CommandType.SET_THING, onSetThing);
				registerFunction(CommandType.SET_THING_LIST, onSetThingList);
				registerFunction(CommandType.FIND_RESULT, onFindResult);
				registerFunction(CommandType.SET_SPRITE_LIST, onSetSpriteList);
			}
			
			//--------------------------------------
			// Public
			//--------------------------------------
			
			override public function onProgress(id:uint, loaded:uint, total:uint) : void
			{
				switch(id)
				{
					case ProgressBarID.DAT:
					case ProgressBarID.SPR:
					{
						if (_datSprProgressBar != null)
						{
							_datSprProgressBar.setProgress(id, loaded, total);
						}
						break;
					}
						
					case ProgressBarID.DEFAULT:
					{
						if (_defaultProgressBar != null)
						{
							_defaultProgressBar.setProgress(loaded, total);
						}
						break;
					}
					
					case ProgressBarID.FIND_THING:
					{
						ObjectBuilder(application).setFindThingProgress(loaded, total);
						break;
					}
				}
			}
			
			override public function onError(message:String, stack:String = "", id:uint = 0) : void
			{
				// Close progress bar
				if (_datSprProgressBar != null)
				{
					_datSprProgressBar.close();
					_datSprProgressBar = null;
				}
				
				// Show error
				if (_errorAlert == null)
				{
					_errorAlert = Alert.show(message, "Error", Alert.OK, UIComponent(application), errorAlertCloseHandler);
				}
				else 
				{
					_errorAlert.text += "\n" + message;
				}
				
				ObjectBuilder(application).appendLog(message);
				
				if (isNullOrEmpty(stack))
				{
					stack = message;
				}
				
				// Save on log file.
				_logger.error(stack);
			}
			
			//--------------------------------------
			// Private
			//--------------------------------------
			
			private function onShowMessage(text:String, title:String) : void
			{
				if (title != "log")
				{
					Alert.show(text, title, Alert.OK, UIComponent(application));
				}
				ObjectBuilder(application).appendLog(text);
			}
			
			private function onLoadComplete() : void
			{
				ObjectBuilder(application).clear();
				
				sendCommand(new Command(CommandType.GET_ASSETS_INFO));
				sendCommand(new GetSpriteListCommand(0));
				sendCommand(new GetThingCommand(100, ThingCategory.ITEM));
			}
			
			public function onSetAssetsInfo(info:AssetsInfo) : void
			{
				ObjectBuilder(application).setAssetsInfo(info);
			}
			
			private function onSetThing(thing:ThingType, list:Vector.<SpriteData>) : void
			{
				ObjectBuilder(application).setThing(thing, list);
			}
			
			private function onSetThingList(target:uint, min:uint, max:uint, list:Vector.<ThingListItem>) : void
			{	
				ObjectBuilder(application).setThingList(target, min, max, list);
			}
			
			private function onFindResult(list:Array) : void
			{
				ObjectBuilder(application).setFindThingResultList(list);
			}
			
			private function onSetSpriteList(target:uint, min:uint, max:uint, list:Vector.<SpriteData>) : void
			{	
				ObjectBuilder(application).setSpriteList(target, min, max, list);
			}
			
			private function showDatSprProgressBar(title:String) : void
			{
				if (_datSprProgressBar == null)
				{
					_datSprProgressBar = new DatSprProgressBar();
					_datSprProgressBar.title = title;
				}
				_datSprProgressBar.open();
			}
			
			private function hideDatSprProgressBar() : void
			{
				if (_datSprProgressBar != null)
				{
					_datSprProgressBar.close();
					_datSprProgressBar = null;
				}
			}
			
			private function showDefaultProgressBar(title:String) : void
			{
				if (_defaultProgressBar == null)
				{
					_defaultProgressBar = new DefaultProgressBar();
					_defaultProgressBar.title = title;
				}
				_defaultProgressBar.open();
			}
			
			private function hideDefaultProgressBar() : void
			{
				if (_defaultProgressBar != null)
				{
					_defaultProgressBar.close();
					_defaultProgressBar = null;
				}
			}
			
			//--------------------------------------
			// Override Protected
			//--------------------------------------
			
			protected function invokeHandler(event:InvokeEvent) : void
			{
				var path : String;
				
				if (event.arguments != null && event.arguments.length > 0)
				{
					path = event.arguments[0];
					if (!isNullOrEmpty(path))
					{
						_directory = FileUtils.getDirectory(new File(path));
					}
				}
				
				if (application != null && UIComponent(application).initialized)
				{
					ObjectBuilder(application).onOpenFile(_directory);
				}
			}
			
			protected function applicationCreationCompleteHandler(event:FlexEvent) : void
			{
				if (_directory != null)
				{
					ObjectBuilder(application).onOpenFile(_directory);
				}
			}
			
			protected function errorAlertCloseHandler(event:CloseEvent) : void
			{
				_errorAlert = null;
			}
			
			override protected function createApplicationInstance() : INailApplication
			{
				var app : ObjectBuilder;
				app = new ObjectBuilder();
				app.addEventListener(FlexEvent.CREATION_COMPLETE, applicationCreationCompleteHandler);
				return app;
			}
			
			override protected function createWorkerByteClass() : ByteArray
			{
				return new ObjectBuilderWorker();
			}
			
		]]>
	</fx:Script>
</nail:NailMain>
